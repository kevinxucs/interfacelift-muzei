buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.9.+'
    }
}

apply plugin: 'android'

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.google.android.apps.muzei:muzei-api:+'
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.3"

    Properties versionProps = new Properties()
    versionProps.load(new FileInputStream(file('version.properties')))

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 19

        versionName versionProps['name']
        versionCode versionProps['code'].toInteger()
    }

    signingConfigs {
        release {
            Properties localProps = new Properties()
            localProps.load(new FileInputStream(file('local.properties')))
            Properties keyProps = new Properties()
            assert localProps['keystore.props.file'];
            keyProps.load(new FileInputStream(file(localProps['keystore.props.file'])))
            storeFile file(keyProps["store"])
            keyAlias keyProps["alias"]

            Console console = System.console();
            if (console != null) {
                storePassword new String(console.readPassword("\nEnter keystore password: "))
                keyPassword new String(console.readPassword("Enter key password: "))
            }
        }
    }

    buildTypes {
        release {
            runProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt')
            signingConfig signingConfigs.release
        }
    }
}
